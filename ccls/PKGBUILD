# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Fangrui Song <i at maskray.me>

pkgname=ccls
pkgver=0.20250815
pkgrel=2
pkgdesc='C/C++/ObjC language server supporting cross references, hierarchies, completion and semantic highlighting'
arch=('x86_64' 'powerpc')
url='https://github.com/MaskRay/ccls'
license=('Apache-2.0')
depends=(
  'clang'
  'gcc-libs'
  'glibc'
  'llvm-libs'
)
makedepends=(
  'cmake'
  'llvm'
  'ninja'
  'rapidjson'
)
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('89ebf24eb4f06d02f09f49e0ef5677008f11b944e27fd9c4169ebba1665dc32488b010a81b930978bcc001454b85617501f87993d59307e159587572bb5c2c19')

build() {
  local cmake_options=(
    -B build -S "$pkgname-$pkgver"
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -W no-dev
    -G Ninja
    -D CMAKE_CXX_COMPILER=clang++
    -D CLANG_LINK_CLANG_DYLIB=ON
    -D CCLS_VERSION="$pkgver"
  )

  cmake "${cmake_options[@]}"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
