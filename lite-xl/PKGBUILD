# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: jgmdev <jgmdev at gmail dot com>
# Contributor: TÃ©rence Clastres <t dot clastres at gmail dot com>
# Contributor: redtide <redtid3 at gmail dot com>

pkgname=lite-xl
pkgver=2.1.8
pkgrel=2
pkgdesc='A lightweight text editor written in Lua'
arch=(x86_64 aarch64 'powerpc')
url="https://$pkgname.com"
license=(MIT)
depends=(freetype2
         glibc # libc.so
         lua
         pcre2
         sdl3)
makedepends=(git # used by Meson to fetch submodule, TODO: move to source()
             meson)
conflicts=(lite)
provides=(lite)
_archive="$pkgname-$pkgver"
source=("https://github.com/$pkgname/$pkgname/archive/v$pkgver/$_archive.tar.gz")
sha256sums=('fcaffb946bc60583369cb040d533a4ac18075a6d474d49a2a5ff4bf87e2e9a10')

build() {
	local meson_opts=(
		use_system_lua=true
	)
	arch-meson "$_archive" build ${meson_opts[@]/#/-D }
	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir" --skip-subprojects
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" "$_archive/LICENSE"
}
